
%{
#include <stdio.h>
#include <stdbool.h>
#include <string.h>
#include <ctype.h>
#include "interncomp.h"
#include "node.h"
#include "../extern/vm.h"
#include "../extern/compart.h"
#include "../intern/compartment.h"
#include "../intern/gen.h"
#include "compsl.tab.h"

extern VM *cvm;
extern compart *ccompart;

%}

%%

\/\/.*\n		{}; /*comment*/
\/\* {
  register int c;

  for ( ; ; ) {
     while ( (c = input()) != '*' &&c != EOF ) ;
     if ( c == '*' ) {
       while ( (c = input()) == '*' ) ;
       if ( c == '/' ) break; 
     }
     if ( c == EOF ) {
       compileWarning( "Comment ended in EOF" );
       
       break;
     }
   }
}
declare	     return DECLARE;
cubbyhole    return CUBBY;
cubby    return CUBBY2;
global		return GLOBAL;
int			return INT;
float		return FLOAT;
if			return IF;
else		return ELSE;
while		return WHILE;
break		return BREAK;
continue	return CONTINUE;
;			return SEMI;
,			return COMA;
\{			return OPENB; /*"bracket"*/
\}			return CLOSEB;
\(			return OPENP; /*"parenthesis"*/
\)			return CLOSEP;
\[			return OPENS;
\]			return CLOSES;
\+			return PLUS;
\+\+			return PLUSPLUS;
\+=			return PLUSEQ;
-			return MINUS;
--			return MINUSMINUS;
-=			return MINUSEQ;
\*			return MULT;
\*=			return MULTEQ;
\/			return DIV;
\/=			return DIVEQ;
\%			return MOD;
\%=			return MODEQ;
==			return ISEQ;
!=			return ISNEQ;
=			return ASSIGN;
\>=			return ISGEQ;
\<=			return ISLEQ;
\>			return ISGT;
\<			return ISLT;
!			return NOT;
\<\<			return SHFTL;
\>\>			return SHFTR;
&&		return AND;
& 		return BAND;
&=			return BANDEQ;
\|\|		return OR;
\|		return BOR;
\|=			return BOREQ;
\ 		      	;
\t			;
\n		{ DPRINTF("%4d\n",lineNo); lineNo++; };
[0-9]+  	{ yylval.ival = atoi( yytext ); return INT_LIT; }
[0-9]*\.[0-9]*  { yylval.fval = atof(yytext); return FLOAT_LIT; }
[_a-zA-Z0-9]+   {
	yylval.sval = calloc(strlen(yytext)+1, sizeof(char));
	if(yylval.sval==NULL) { 
		internalCompileError("Out of memory");
	}
	strcpy(yylval.sval,yytext);
	return IDENTIFIER;
}

%%
